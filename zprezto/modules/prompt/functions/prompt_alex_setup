# A modified theme for prezto based on the skwp-theme.

function prompt_alex_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_alex_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_alex_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_alex_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_alex_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_alex_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:clean' format "${_prompt_alex_colors[5]}✔%f"
  zstyle ':prezto:module:git:info:dirty' format "${_prompt_alex_colors[4]}✗✗✗%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b %C%D)'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  # Define prompts.
  _lineup=$'\e[1A'
  _linedown=$'\e[1B'

  PROMPT="╭─${_prompt_alex_colors[3]}%n%f@${_prompt_alex_colors[2]}%m%f ${_prompt_alex_colors[5]}%~%f "'${git_info:+${(e)git_info[prompt]}}'"
╰─$ "
  RPROMPT="%{${_lineup}%}%F{grey}[%D{%H:%M:%S}]%{${_linedown}%}"
}

prompt_alex_setup "$@"
